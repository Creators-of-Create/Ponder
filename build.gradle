plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.1-SNAPSHOT" apply false
}

apply from: 'gradle/property_loader.gradle'
apply from: 'gradle/build_number.gradle'

def projVersion = project.version

architectury {
    minecraft = project.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply plugin: "architectury-plugin"

    architectury {
        compileOnly()
    }

    loom {
        silentMojangMappingsLicense()
    }

    project.version = projVersion

    apply plugin: 'maven-publish'

    apply plugin: 'idea'
    apply plugin: 'eclipse'

    apply from: '../gradle/property_loader.gradle'
    apply from: '../gradle/java.gradle'
    apply from: '../gradle/minify_jsons.gradle'
    apply from: '../gradle/signing.gradle'

    // Enable Mixins
    project.ext.mixin_enabled = project.file("src/main/resources/${mod_id}.mixins.json").exists()
    project.logger.lifecycle("Mixin ${project.ext.mixin_enabled ? 'enabled' : 'disabled'} for project ${mod_id}:${project.name}.")

    repositories {
        //mavenLocal()
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' } // Sponge / Mixin
        maven { url = 'https://maven.blamejared.com' } // BlameJared Maven (CrT / Bookshelf)
        maven { url = 'https://maven.terraformersmc.com/releases/' } // modmenu
        maven { url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/" } // Fuzs Mod Resources
        maven { url = "https://jitpack.io" }
        maven { url = "https://mvn.devos.one/snapshots/" } // Jay's Maven
        maven { url = 'https://maven.tterrag.com' } // Registrate and Flywheel
        maven { url = "https://maven.createmod.net" } // Catnip and Ponder

    }
}
